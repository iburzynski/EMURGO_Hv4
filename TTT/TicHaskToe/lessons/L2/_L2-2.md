# **Lesson 2-2: Complex Functions**
* Lexical Scope
* Multi-parameter Functions
  * Currying
  * Partial Application
* Patterns & Destructuring

## **Lexical Scope**

## **Multi-parameter Functions**
The number of parameters/arguments a function has is called its **arity**. So far we've only defined constants, which have arity `0`, and **unary** functions, which have arity `1`. We've also used some binary functions in the form of infix operators (`(+)`, `(-)`, `(&&)`, `(||)`, `(:)`, `(++)`).

|Arity | Name|
--- | ---|
| 0 | constant |
| 1 | unary function |
| 2 | binary function |
| x | arity-x function |

It turns out that when we cross the threshold from unary to higher arity functions in Haskell, things get very interesting.

Let's take a closer look at a simple binary function to see why.

**Version 1:**
```haskell
(&&) :: Bool -> Bool -> Bool
(&&) b1 b2 = if b1 then b2 else False

ex1 = (&&) True False
```

At a glance, this function appears to consume two arguments at the same time and return a `Bool` value. But you may notice that its signature looks a bit odd: we can surmise that the first two `Bool` values correspond to its input parameters, and the last one to its return value. But it's strange that the inputs are separated by an arrow, which we're used to separating input from output.

It might seem more sensible to write the signature as
```haskell
(&&) :: (Bool, Bool) -> Bool
```
But this would be a different function: a unary function that takes a tuple value as its sole input. This is more like the behavior of most languages, in which functions consume some single collection of arguments (which may be of determinate or variable length).

Haskell functions don't behave this way, and we'll see how this allows for greater flexibility and code reuse.

### **Currying**
An interesting feature of Haskell is that technically all Haskell functions are unary. In practice we can treat functions as having multiple parameters/arguments, but in reality they only consume one argument at a time. If they need to consume multiple pieces of data in order to produce some final value, then they return new intermediate functions, which in turn continue consuming inputs one at a time until all the inputs have been exhausted.

Functions that behave this way are called **curried**, and the practice of writing such functions is called **currying**.

Let's rewrite the `(&&)` function with explicit currying

**Version 2 with Explicit Currying:**
```haskell
(&&) :: Bool -> (Bool -> Bool)
(&&) b1 = f -- `(&&)` consumes one argument `b1` and returns a new function `f`, which will consume the next argument `b2`
  where
    -- `f` "closes over" its parent function, capturing whichever value of `b1` it was applied to for later use
    f b2 = if b1 then b2 else False
    --        ^ captured variable

ex2 = ((&&) True) False
--               ^ `f` is then applied to `False`
--     ^ this call produces the function `f`
```

Haskell functions aren't actually defined this way in practice:
  * All functions are curried by default: any intermediate functions and associated closures get created automatically rather than needing to be explicitly defined, and the intermediate functions are anonymous
  * In signatures, the parentheses on the right side of arrows are implicit and don't need to be written
  * In function calls, parentheses are also implicit and don't need to be written (`((&&) True) False` is the same as `(&&) True False`)

In other words, **Version 2** of our function behaves identically to **Version 1** in every way, so we opt for the "sugared" syntax of Version 1. But it's important to understand that Version 2 more faithfully resembles how the code is executed, because it will unlock unique ways of thinking about and structuring code.

### **Partial Application**
The main benefit of curried functions is that they give us the flexibility to call a function with less than its total number of arguments. While such **partial application** in other languages would give us an error or return an undefined value, in Haskell we receive new functions that are specialized with one or more pre-applied inputs.

This lets us write general-use functions and easily generate more specific variations of them on the fly without repeating ourselves.
```haskell
printMove :: Pokemon -> Move -> String
printMove p m = p ++ " used " ++ m ++ "!"

pmPikachu = printMove "PIKACHU"
pmCharmander = printMove "CHARMANDER"
```
>We can generate messages for a specific Pokemon by pre-applying it to `printMove`, then applying the resulting function to various moves:
```shell
GHCi> pmPikachu "THUNDERBOLT"
"PIKACHU used THUNDERBOLT!"
GHCi> pmPikachu "BODY SLAM"
"PIKACHU used BODY SLAM!"
GHCi> pmCharmander "EMBER"
"CHARMANDER used EMBER!"
```
We don't need to define these partially-applied variants as standalone functions: in practice they're often created inline in an ad-hoc fashion.

## **Patterns & Destructuring**
Haskell makes it easy to match on the shapes of various collection types and **destructure** them into their components. The destructured components can be assigned variable names for reference elsewhere in our code.

```haskell
getRowIndex :: (Int, Int) -> Int
getRowIndex (i, j) = i

getColIndex :: (Int, Int) -> Int
getColIndex (i, j) = j
```
>Haskell has built-in functions `fst` and `snd` that return the first and second elements of any 2-tuple, so we don't need to define these functions ourselves. But it's still convenient to destructure like this in many situations.

```haskell
type Point3D = (Int, Int, Int)

getDepth :: Point3D -> Int
getDepth (x, y, z) = z
```

**Wildcard Pattern** \
If we don't need all the parts inside the collection, we don't have to assign them variable names. The `_` symbol acts as a special wildcard to indicate an unused value.

```haskell
getRowIndex :: (Int, Int) -> Int
getRowIndex (i, _) = i

getColIndex :: (Int, Int) -> Int
getColIndex (_, j) = j

getDepth :: Point3D -> Int
getDepth (_, _, z) = z
```

### **Destructuring Lists**
We can use literal patterns to match on the size of a list and destructure it into its individual elements.
```haskell
describeList :: [a] -> String
describeList []        = "empty list"
describeList [x]       = "singleton list"
describeList [x, y]    = "2 element list"
describeList [x, y, z] = "3 element list"
describeList _         = "4+ element list"
```
>In this example we aren't using the values of the list elements at all, so we can replace all the variables with wildcard placeholders and achieve the same effect:
```haskell
describeList []        = "empty list"
describeList [_]       = "singleton list"
describeList [_, _]    = "2 element list"
describeList [_, _, _] = "3 element list"
...
```
A different pattern allows us to match on any non-empty list by using the "cons" constructor. Recall that any non-empty list consists of a "head" element "cons'ed" to a "tail" list.

```haskell
describeContents (x:xs) = concat [
    "The head is "
  , show x
  , " and the tail is "
  , show xs
  ]
describeContents [] = "The list is empty"
```
>Note the use of *parentheses* in this destructuring technique, *not* square brackets. These parentheses are not part of the structure here like when we destructure tuples: they are just a cue to the compiler to associate what's inside them as a single value.

We can use the "cons" constructor multiple times to  additional elements
```haskell
secondElem :: Show a => [a] -> String
secondElem (x:y:zs) = "The 2nd element is " ++ show y
secondElem _ = "List has less than two elements"
```

### **Pattern Order**
Patterns must be ordered from *most specific* to *most general*. Otherwise the more general pattern will match on all inputs before a more specific pattern has a chance to be attempted.
```haskell
brokenSecondElem _        = "List has less than two elements"
brokenSecondElem (x:y:zs) = "The 2nd element is " ++ show y
```
>The wildcard pattern is the most general pattern, and matches on any possible input. This broken function will return "List has less than two elements" even if it is applied to a two element list.

```haskell
brokenDescribeList []    = "empty"
brokenDescribeList (_:_) = "two or more elements"
brokenDescribeList [_]   = "singleton"
```
>This broken function will return "two or more elements" when applied to a singleton list, because the `[_]` pattern is more specific than the `(_:_)` pattern, which matches on any non-empty list.

### **Exhaustive Patterns**
To avoid runtime errors, we need to ensure our functions cover all possible inputs they might receive. This means defining an  **exhaustive** list of patterns.

```haskell
-- inexhaustive `describeContents`
describeContents (x:xs) = concat [
    "The head is "
  , show x
  , " and the tail is "
  , show xs
  ]
```
>This incomplete version of `describeContents` will throw an "inexhaustive patterns" error if we apply it to an empty list.

```haskell
-- inexhaustive `secondElem`
secondElem (x:y:zs) = "The 2nd element is " ++ show y
```
>This incomplete version of `secondElem` will throw an "inexhaustive patterns" error if we apply it to an empty or singleton list.